# Serpent

```elixir
my_app_root = Path.join(__DIR__, "graphics")

Mix.install(
  [
    {:graphics, path: my_app_root, env: :dev}
  ],
  # config_path: Path.join(my_app_root, "config/config.exs"),
  lockfile: Path.join(my_app_root, "mix.lock")
)
```

## Game Module

```elixir
import IEx.Helpers
```

```elixir
defmodule Game do
  @segments [{10, 10}, {9, 10}, {8, 10}, {7, 10}, {6, 10}, {5, 10}]
  defstruct serpent: @segments,
            direction: :east,
            apples: [{3, 3}],
            turns: []

  def new(), do: %__MODULE__{} |> set_random_apple()

  def tick(%Game{} = game) do
    game
    |> maybe_turn()
    |> move()
    |> maybe_eat_apple()
  end

  def set_random_apple(%Game{serpent: serpent} = game) do
    random_apple =
      Stream.repeatedly(fn -> {Enum.random(0..19), Enum.random(0..19)} end)
      |> Stream.drop_while(fn {_x, _y} = segment -> segment in serpent end)
      |> Enum.take(1)
      |> hd()

    %Game{game | apples: [random_apple]}
  end

  defp maybe_eat_apple(%Game{serpent: serpent, apples: apples} = game) do
    apple_to_eat = Enum.filter(apples, &(&1 == hd(serpent)))

    if apple_to_eat == [] do
      game
    else
      remaining_apples = Enum.reject(apples, &(&1 == apple_to_eat))

      %Game{game | apples: remaining_apples}
      |> Game.set_random_apple()
      |> Game.extend_serpent()
    end
  end

  def extend_serpent(%Game{serpent: serpent} = game) do
    tail_direction = tail_direction(serpent)
    extend_position = extend_position(List.last(serpent), tail_direction)
    new_serpent = List.insert_at(serpent, -1, extend_position)
    %Game{game | serpent: new_serpent}
  end

  def tail_direction(serpent) do
    [{x_before_last, y_before_last}, {x_last, y_last}] =
      Enum.drop(serpent, Enum.count(serpent) - 2)

    if x_before_last == x_last do
      if y_before_last > y_last,
        do: :north,
        else: :south
    else
      if x_before_last > x_last,
        do: :east,
        else: :west
    end
  end

  def extend_position({x, y} = _tail, :west), do: {x + 1, y}
  def extend_position({x, y} = _tail, :east), do: {x - 1, y}
  def extend_position({x, y} = _tail, :north), do: {x, y + 1}
  def extend_position({x, y} = _tail, :south), do: {x, y - 1}

  defp maybe_turn(%Game{turns: []} = game), do: game

  defp maybe_turn(%Game{turns: [next_turn | remaining_turns]} = game) do
    next_game = %Game{game | turns: remaining_turns}
    Game.turn(next_game, next_turn)
  end

  defp serpent_length(), do: Enum.count(@segments)

  def add_turn(%Game{} = game, turn) do
    %Game{game | turns: game.turns ++ [turn]}
  end

  def move(%Game{direction: :east, serpent: [{x, y} | _]} = game),
    do: move_to(game, {rem(x + 1, 20), y})

  def move(%Game{direction: :west, serpent: [{x, y} | _]} = game) do
    x_to =
      if x == 0,
        do: 19,
        else: x - 1

    move_to(game, {x_to, y})
  end

  def move(%Game{direction: :north, serpent: [{x, y} | _]} = game) do
    y_to =
      if y == 0,
        do: 19,
        else: y - 1

    move_to(game, {x, y_to})
  end

  def move(%Game{direction: :south, serpent: [{x, y} | _]} = game),
    do: move_to(game, {x, rem(y + 1, 20)})

  defp move_to(%Game{} = game, {x, y}) do
    serpent =
      [{x, y} | game.serpent]
      |> Enum.take(serpent_length() - 1)

    %Game{game | serpent: serpent}
  end

  def turn(%Game{direction: :east} = game, :left), do: %Game{game | direction: :north}
  def turn(%Game{direction: :east} = game, :right), do: %Game{game | direction: :south}
  def turn(%Game{direction: :west} = game, :left), do: %Game{game | direction: :south}
  def turn(%Game{direction: :west} = game, :right), do: %Game{game | direction: :north}
  def turn(%Game{direction: :north} = game, :left), do: %Game{game | direction: :west}
  def turn(%Game{direction: :north} = game, :right), do: %Game{game | direction: :east}
  def turn(%Game{direction: :south} = game, :left), do: %Game{game | direction: :east}
  def turn(%Game{direction: :south} = game, :right), do: %Game{game | direction: :west}
  def turn(%Game{direction: :none} = game), do: game

  def render(game, render_fn), do: render_fn.(%{apples: game.apples, segments: game.serpent})
end
```

## User Interface API

```elixir
alias Graphics.SVG
alias Graphics.SVG.Rectangle

border_horizontal_coords = for x <- 0..199, y <- [0, 199], do: {x, y}
border_vertical_coords = for x <- [0, 199], y <- 0..199, do: {x, y}
border_coords = border_horizontal_coords ++ border_vertical_coords

Enum.reduce(border_coords, SVG.new([], 200, 200), fn {x, y}, svg ->
  SVG.add(svg, Rectangle.draw(x: x, y: y, width: 1, height: 1))
end)
|> SVG.render()
```

```elixir
defmodule Renderer do
  alias Graphics.SVG
  alias Graphics.SVG.Rectangle

  def draw(opts) do
    SVG.new([], 200, 200)
    |> add_rectangles(opts.apples, :red)
    |> add_rectangles(opts.segments, :blue)
    |> add_border(:black)
    |> SVG.render()
  end

  def add_rectangles(svg, points, color) do
    Enum.reduce(points, svg, fn {_x, _y} = point, acc ->
      add_rectangle(acc, point, color)
    end)
  end

  def add_rectangle(svg, {x, y}, color) do
    SVG.add(
      svg,
      Rectangle.draw(x: x * 10, y: y * 10, width: 10, height: 10, fill: color)
    )
  end

  def add_border(svg, color) do
    border_horizontal_coords = for x <- 0..199, y <- [0, 199], do: {x, y}
    border_vertical_coords = for x <- [0, 199], y <- 0..199, do: {x, y}
    border_coords = border_horizontal_coords ++ border_vertical_coords

    Enum.reduce(border_coords, svg, fn {x, y}, acc ->
      SVG.add(acc, Rectangle.draw(x: x, y: y, width: 1, height: 1, fill: color))
    end)
  end
end
```

```elixir
Renderer.draw(%{apples: [{1, 1}], segments: [{2, 2}, {2, 1}, {2, 3}, {2, 4}]})
```

```elixir
Game.new()
|> Game.turn(:left)
|> Game.move()
|> Game.extend_serpent()
|> Game.extend_serpent()
|> Game.extend_serpent()
|> Game.render(&Renderer.draw/1)
```

## Kino Animate

```elixir
keyboard = Kino.Control.keyboard([:keydown])
Kino.Control.subscribe(keyboard, :keyboard)

Kino.animate(500, Game.new(), fn _event, game ->
  # doesnÂ´t work, because the keystroke must be received from a different process
  # keystroke = Keyboardhandler.process()

  next_game =
    Game.move(game)
    |> Game.turn(:right)

  frame = Game.render(next_game, &Renderer.draw/1)
  {:cont, frame, next_game}
end)

keyboard
```

```elixir
Game.new()
|> Game.turn(:right)
|> Game.move()
|> Game.render(&Renderer.draw/1)
```

## Keyboard Prototype

```elixir
defmodule Keyboardhandler do
  def process() do
    receive do
      {:keyboard, %{key: "ArrowLeft"}} -> :left
      {:keyboard, %{key: "ArrowRight"}} -> :right
    after
      1 -> :none
    end
  end
end
```

```elixir
keyboard = Kino.Control.keyboard([:keydown])
Kino.Control.subscribe(keyboard, :keyboard)

keyboard
```

```elixir
Kino.Control.subscribe(keyboard, keyboard)

Keyboardhandler.process()
```

## OTP Server

Create OTP game server with CRC

- construct: `init` that takes a frame, create a game, render the game, start a timer to: `move`
- reduce: `handle_call` to `move` and `handle_cast` to `turn`
- convert: part of `handle_call` to `render` the `game`

```elixir
defmodule Server do
  use GenServer

  def init({frame, game_keyboard}) do
    Kino.Control.subscribe(game_keyboard, :keyboard)

    game = Game.new()
    render(frame, game)
    :timer.send_interval(250, :game, :move)
    {:ok, {frame, game}}
  end

  def render(frame, %Game{} = game) do
    svg = Game.render(game, &Renderer.draw/1)
    Kino.Frame.render(frame, svg)
  end

  def handle_info(:move, {frame, %Game{} = game}) do
    next_game = Game.tick(game)
    render(frame, next_game)
    {:noreply, {frame, next_game}}
  end

  def handle_info({:keyboard, %{key: "ArrowRight"}}, {frame, %Game{} = game}) do
    {:noreply, {frame, Game.add_turn(game, :right)}}
  end

  def handle_info({:keyboard, %{key: "ArrowLeft"}}, {frame, %Game{} = game}) do
    {:noreply, {frame, Game.add_turn(game, :left)}}
  end

  def handle_info({:keyboard, _other_key}, {frame, %Game{} = game}) do
    {:noreply, {frame, game}}
  end

  def start_link({frame, game_keyboard}) do
    GenServer.start(__MODULE__, {frame, game_keyboard}, name: :game)
  end

  def go(game_keyboard) do
    frame = Kino.Frame.new()

    Kino.start_child!({__MODULE__, {frame, game_keyboard}})
    frame
  end
end
```

## Run Server

```elixir
keyboard = Kino.Control.keyboard([:keydown])
```

```elixir
Server.go(keyboard)
```

## Kill Server

```elixir
:game
|> GenServer.whereis()
|> Process.exit(:unscheduled)
```

## Experiment with a random apple

```elixir
random_apple = fn -> {:random.uniform(20), :random.uniform(20)} end
```

```elixir
serpent = for x <- 1..19, y <- 1..20, do: {x, y}
```

```elixir
Stream.repeatedly(fn -> {:rand.uniform(20), :rand.uniform(20)} end)
|> Stream.drop_while(fn {_x, _y} = segment -> segment in serpent end)
|> Enum.take(1)
|> hd()
```
